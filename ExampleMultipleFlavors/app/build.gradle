apply plugin: 'com.android.application'

android {
    compileSdkVersion 19
    buildToolsVersion "19.1.0"

    defaultConfig {
        applicationId "net.simplyadvanced.examplemultipleflavors"
        minSdkVersion 15
        targetSdkVersion 19
        versionCode 1
        versionName "0.1"
    }

    productFlavors {
        pro {
            applicationId "net.simplyadvanced.examplemultipleflavors.pro"
            manifestPlaceholders = [
                    mainActivityLabel: "MainActivityLabel Pro",
                    exampleMetaData: "Pro meta data"
            ]
        }
        free {
            applicationId "net.simplyadvanced.examplemultipleflavors.free"
            manifestPlaceholders = [
                    mainActivityLabel: "MainActivityLabel Free",
                    exampleMetaData: "Free meta data"
            ]
        }
    }

    signingConfigs {
        release {
            storeFile     file(STORE_FILE)
            storePassword STORE_PASSWORD
            keyAlias      KEY_ALIAS
            keyPassword   KEY_PASSWORD
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            runProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            // This is used to alter output directory and file name. If you don't need it
            // you can safely comment it out.
            applicationVariants.all { variant ->
                def file = variant.outputFile

                String parent = file.parent
                if (project.hasProperty('OUTPUT_DIR') && new File((String) OUTPUT_DIR).exists())
                    parent = OUTPUT_DIR

                variant.outputFile = new File(parent, (String) file.name.replace(".apk",

                    // Alter this string to change output file name.
                    "-" + defaultConfig.versionName + "-build" + defaultConfig.versionCode + ".apk"
                ))
            }
            // End your comment here.
        }
        debug {
            applicationIdSuffix ".debug"
        }
    }

}

dependencies {
    compile 'com.jakewharton:butterknife:5.1.1'
    compile fileTree(dir: 'libs', include: ['*.jar'])
}
